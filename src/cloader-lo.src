;  cloader - Config loader for FujiNet, loads CONFIG.COM using HSIO routines
;  2021 apc.atari@gmail.com
;
;  This program is free software; you can redistribute it and/or modify
;  it under the terms of the GNU General Public License as published by
;  the Free Software Foundation; either version 2 of the License, or
;  (at your option) any later version.
;
;  This program is distributed in the hope that it will be useful,
;  but WITHOUT ANY WARRANTY; without even the implied warranty of
;  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;  GNU General Public License for more details.
;
;  You should have received a copy of the GNU General Public License
;  along with this program; if not, write to the Free Software
;  Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.

; useful labels
	.include "hisio.inc"
        
; lz4bootloader labels
DOLOAD  = $4B0
SECLO   = $4B1
SECHI   = $4B3
JMPRUNV = $51C
JMPSIO  = $5D3
BUF     = $700

; some "colors"
        * = $02C4
        .BYTE $06, $0C, $04, $00, $04

; ; place our code after picoboot loader ($980)
;         * = PICOEND
        * = $800

; Config start sector - update with real value !!!
CFGSSEC .WORD 14

; progress bar speed factor
; use cloader-updater.sh to update this field in compiled file, no need to recompile
;
;   px:   size of progress bar in pixels
;   blks: number of blocks to load
;   = (1 + px) * 256 / blks = (1 + 48) * 256 / 175 = 71
; PBSF    .BYTE 71
CFGSIZE = 11243 ; 21865 ; config.com file size in bytes
PBSF    .BYTE 12544/((CFGSIZE+124)/125) ; assuming SD

; include HSIO code
    .IF .DEF HIGHSPEED
	SIOADR = *
	FASTVBI=1
	HISIO_MYPICODOS=1
	.include "hisiocode.src"
	SIOEND = *
; HSIO index retrieved from D1
; HISAV   .BYTE $FF       ; >=128 no high speed
HISAV   = SIOSPEED
SIOSPEED .BYTE $FF
    .ENDIF

PBOF    .BYTE 0                 ; progress bar offset to current byte
PBMASK  .BYTE $C0               ; progress bar pixel mask
SEC2PB  .BYTE 0                 ; sector count to progress bar update
DLSAV   .WORD 0                 ; original display list address
CLNVEC  .WORD CLEANUP           ; our RUN hook, for cleanup prior starting Config

SIOCMDS
    .IF .DEF HIGHSPEED
GETHICMD
	.BYTE $31		; DDEVIC = D
	.BYTE $01		; DUNIT = D1
	.BYTE $3F		; DCOMND = Get high speed
	.BYTE $40		; DSTATS = -> ATARI
	.WORD HISAV 		; DBUF
	.BYTE $03		; DTIMLO = 3 seconds
	.BYTE $00		; DRESVD = $00
	.WORD 1              	; one byte
	.WORD $0000		; DAUX = 0
    .ENDIF

; SETHICMD
; 	.BYTE $70		; DDEVIC = $70 (Fuji)
; 	.BYTE $01		; DUNIT = 1
; 	.BYTE $E3		; DCOMND = Set high speed
; 	.BYTE $40		; DSTATS = -> ATARI
; 	.WORD $0000 		; DBUF
; 	.BYTE $03		; DTIMLO = 3 seconds
; 	.BYTE $00		; DRESVD = $00
; 	.WORD 0			; no data bytes
; HIVAL	.WORD $0000		; AUX1 = 0 - index, AUX2 = 0 - do not save

; READCMD
; 	.BYTE $31		; DDEVIC = D
; 	.BYTE $01		; DUNIT = D1
; 	.BYTE $52		; DCOMND = Read
; 	.BYTE $40		; DSTATS = -> ATARI
; 	.WORD BUF 		; DBUF
; 	.BYTE $07		; DTIMLO = 7 seconds
; 	.BYTE $00		; DRESVD = $00
; 	.WORD $0080            	; 128 bytes
; 	.WORD $0169		; DAUX = sector #

CMDSAVE
	.WORD 0 		; DBUF
	.BYTE 0 		; DTIMLO
	.BYTE 0		        ; DRESVD
	.WORD 0            	; AUX1/2

; Config Loader code
CLOADER
    .IF .DEF HIGHSPEED
        ; save SIO parameters for boot loader
        LDX #5
?SAV1   LDA DBUFLO,X
        STA CMDSAVE,X
        DEX
        BPL ?SAV1
        ; get HSIO index, to HISAV
        LDX #GETHICMD-SIOCMDS+11
        JSR CALLSIO
        BMI ?NOHS1
        ; ; set index to 0
        ; LDA #0
        ; STA SIOSPEED
        ; LDX #SETHICMD-SIOCMDS+11
        ; JSR CALLSIO
        ; BPL ?CL1
        ; ; failed to set HSIO index to 0, use what we get
        ; LDA HISAV
        ; STA SIOSPEED
; ?CL1
        ; redirect SIO to HSIO, patch COM/LZ4 loader
        LDA #<DOHISIO
        STA NE459+1
        STA JMPSIO+1
        LDA #>DOHISIO
        STA NE459+2
        STA JMPSIO+2
?NOHS1
        ; restore SIO parameters
        LDX #5
?RES1   LDA CMDSAVE,X
        STA DBUFLO,X
        DEX
        BPL ?RES1
    .ENDIF

        ; pass control to boot loader code
        RTS

CALLSIO LDY #11
?LO1    LDA SIOCMDS,X
        STA DDEVIC,Y
        DEX
        DEY
        BPL ?LO1
NE459   JMP $E459       ; will be updated to call HSIO

        CLLOEND = *

    .IF .NOT .DEF PARTHI
; get labels from high speed part
	.OPT NO OBJ
        .include "cloader-hi.src"
	.OPT OBJ
    .ENDIF

; init address
	* = $02E2
	.WORD CLOADER
